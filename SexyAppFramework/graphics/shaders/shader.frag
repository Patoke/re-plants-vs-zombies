#version 450

layout(binding = 0) uniform sampler2D texSampler;

layout(location = 0) in vec2 fragTexCoord;
layout(location = 1) in vec4 fragColor;

layout(location = 0) out vec4 outColor;

layout(push_constant) uniform constants {
    vec4 vertices[4];
    uint colors[4];
    vec4 renderColor;
    bool isQuad;
    bool toFilter;
} PushConstants;


// from http://www.java-gaming.org/index.php?topic=35123.0
vec4 cubic(float v) {
    vec4 n = vec4(1.0, 2.0, 3.0, 4.0) - v;
    vec4 s = n * n * n;
    float x = s.x;
    float y = s.y - 4.0 * s.x;
    float z = s.z - 4.0 * s.y + 6.0 * s.x;
    float w = 6.0 - x - y - z;
    return vec4(x, y, z, w) * (1.0/6.0);
}

vec4 premultiplyAlpha(vec4 c) {
    return vec4(c.w*c.xyz, c.w);
}

vec4 unpremultiplyAlpha(vec4 c) {
    return vec4(c.w == 0 ? vec3(0.0f) : c.xyz/c.w, c.w);
}

vec4 textureBicubic(sampler2D samp, vec2 texCoords) {
    vec2 texSize = textureSize(samp, 0);
    vec2 invTexSize = 1.0 / texSize;

    texCoords = texCoords * texSize - 0.5;

    vec2 fxy = fract(texCoords);
    texCoords -= fxy;

    vec4 xcubic = cubic(fxy.x);
    vec4 ycubic = cubic(fxy.y);

    vec4 c = texCoords.xxyy + vec2 (-0.5, +1.5).xyxy;
    
    vec4 s = vec4(xcubic.xz + xcubic.yw, ycubic.xz + ycubic.yw);
    vec4 offset = c + vec4 (xcubic.yw, ycubic.yw) / s;
    
    offset *= invTexSize.xxyy;
    
    vec4 sample0 = premultiplyAlpha(texture(samp, offset.xz));
    vec4 sample1 = premultiplyAlpha(texture(samp, offset.yz));
    vec4 sample2 = premultiplyAlpha(texture(samp, offset.xw));
    vec4 sample3 = premultiplyAlpha(texture(samp, offset.yw));

    float sx = s.x / (s.x + s.y);
    float sy = s.z / (s.z + s.w);

    return mix(mix(sample3, sample2, sx), mix(sample1, sample0, sx), sy);
}

void main() {
    vec4 tex;

    if (PushConstants.toFilter)
        tex = unpremultiplyAlpha(textureBicubic(texSampler, fragTexCoord));
    else
        tex = texture(texSampler, fragTexCoord);

    outColor = fragColor * tex;
}
